file = { SOI ~ (subtree_or_thing)* ~ EOI }

subtree_or_thing = _{ subtree | thing }

thing = { ident | number | punctuation }
ident = @{ ident_leader ~ ident_remainder* }

ident_leader = { ASCII_ALPHA | "_" }
ident_remainder = { ASCII_ALPHANUMERIC | "_" }

number = { float | int }

float = _{ float_64 }
float_64 = @{ float_span }

int = _{ int_b10_up }
int_b10_up = @{ digit_span }

float_span = _{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+) }
digit_span = _{ ASCII_DIGIT+ }

punctuation = { "..." | ".." | "." |
			    "!" | "::" | ":" | ";" |
                "|" | "+" | "-" | "/" | "*" |
				"#" | "~" | "`" | "$" |
				"==" | "=" | "," | "@" | "?" }

subtree = { (paren_subtree | brace_subtree | square_subtree | angle_subtree ) }
paren_subtree = { "(" ~ (subtree_or_thing)* ~ ")" }
brace_subtree = { "{" ~ (subtree_or_thing)* ~ "}" }
square_subtree = { "[" ~ (subtree_or_thing)* ~ "]" }
angle_subtree = { "<" ~ (subtree_or_thing)* ~ ">" }


WHITESPACE = _{ " " | "\n" | "\r\n" }